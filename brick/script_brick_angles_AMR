## Gnuplot script for the "Brick experiment" results
## of Glerum et al. 2017.

## We add text in Inscape, thus output as svg
#set term svg font "Times-New-Roman,14"
set term pngcairo font "Times-New-Roman,14"
set termoption dash

## Set line styles
set style line 1 lc rgb 'gray70' lt 1 lw 3 pt 7 pi -1 ps 1.5
set style line 2 lc rgb 'gray50' lt 1 lw 3 pt 7 pi -1 ps 1.5
set style line 10 lc rgb 'gray50' lt 1 lw 3 pt 9 pi -1 ps 1.5
set style line 3 lc rgb 'gray30' lt 1 lw 3 pt 7 pi -1 ps 1.5
set style line 4 lc rgb 'black' lt 1 lw 2 
set style line 5 lc rgb 'black' dt 2 lw 2 
set style line 6 lc rgb 'black' dt 3 lw 2 
set style line 7 lc rgb 'red' lt 1 lw 3 pt 7 pi -1 ps 1.5
set style line 8 lc rgb 'red' lt 1 lw 3 pt 7 pi -1 ps 1.5
set style line 9 lc rgb 'red' lt 1 lw 3 pt 7 pi -1 ps 1.5

## Graph formatting
set xtics 5
set size square
set ytics 5

unset key

set grid lt 1 lc rgb "grey"

unset colorbox

set border lw 3
set pointintervalbox 2.5

set xlabel 'Internal friction angle [{/Symbol \260}]' offset 0,0.5,0 font "Times-New-Roman:Bold, 18"
set ylabel 'Shear band angle [{/Symbol \260}]' offset 0.5,0,0 font "Times-New-Roman:Bold, 18"

## Output file
set output 'brick_angles_AMR.png'

## Plotting
## Unconverged models are colored red
## Grey colors intensify with model mesh resolution
plot [0:35] [25:65] 'angles_theory_extension.dat' u 1:2 w l ls 4, \
                    'angles_theory_extension.dat' u 1:3 w l ls 5, \
                    'angles_theory_extension.dat' u 1:4 w l ls 6, \
                    'angles_theory_compression.dat' u 1:2 w l ls 4, \
                    'angles_theory_compression.dat' u 1:3 w l ls 5, \
                    'extension_AMR_2_angles.dat' u 1:2 with linespoints ls 10, \
                    'compression_AMR_2_angles.dat' u 1:2 with linespoints ls 10, \
                    'compression_AMR_visc_099_2_angles.dat' u 1:2 with linespoints ls 10, \
                    'compression_AMR_vel_095_2_angles.dat' u 1:2 with linespoints ls 10, \
                    'extension_7_angles.dat' u 1:2 with linespoints ls 2, \
                    'compression_7_angles.dat' u 1:2 with linespoints ls 2, \
                    'extension_7_unconv_angles.dat' u 1:2 ls 8, \
                    'compression_7_unconv_angles.dat' u 1:2 ls 8
